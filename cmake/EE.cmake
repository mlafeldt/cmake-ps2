#
# CMake platform file for PS2 EE processor
#
# Copyright (C) 2009-2010 Mathias Lafeldt <misfire@debugon.org>
#

SET(PS2SDK "$ENV{PS2SDK}")
IF (NOT PS2SDK)
    MESSAGE(FATAL_ERROR "PS2SDK env var not set.")
ENDIF (NOT PS2SDK)

INCLUDE_DIRECTORIES("${PS2SDK}/ee/include")
INCLUDE_DIRECTORIES("${PS2SDK}/common/include")

LINK_DIRECTORIES("${PS2SDK}/ee/lib")

SET(EE_CFLAGS "-D_EE -O2 -G0 -Wall" CACHE STRING "EE C compiler flags" FORCE)
SET(EE_LDFLAGS "" CACHE STRING "EE linker flags" FORCE)
SET(EE_ASFLAGS "-G0" CACHE STRING "EE assembler flags" FORCE)

SET(EE_CRT0 "${PS2SDK}/ee/startup/crt0.o" CACHE STRING "EE crt0 file" FORCE)
SET_SOURCE_FILES_PROPERTIES(${EE_CRT0} PROPERTIES EXTERNAL_OBJECT TRUE)

SET(EE_LINKFILE "${PS2SDK}/ee/startup/linkfile" CACHE STRING "EE linkfile" FORCE)


SET_PROPERTY(GLOBAL PROPERTY TARGET_SUPPORTS_SHARED_LIBS TRUE)

SET(CMAKE_C_FLAGS_INIT ${EE_CFLAGS})
SET(CMAKE_CXX_FLAGS_INIT ${EE_CFLAGS})
SET(CMAKE_EXE_LINKER_FLAGS_INIT ${EE_LDFLAGS})

SET(CMAKE_SHARED_LIBRARY_C_FLAGS ${EE_CFLAGS})
SET(CMAKE_SHARED_LIBRARY_CREATE_C_FLAGS "-mno-crt0 -Wl,-r -Wl,-d")
SET(CMAKE_SHARED_LIBRARY_LINK_C_FLAGS)
SET(CMAKE_SHARED_LIBRARY_RUNTIME_C_FLAG)
SET(CMAKE_SHARED_LIBRARY_RUNTIME_C_FLAG_SEP)

SET(CMAKE_LINK_LIBRARY_SUFFIX)
SET(CMAKE_STATIC_LIBRARY_PREFIX "lib")
SET(CMAKE_STATIC_LIBRARY_SUFFIX ".a")
SET(CMAKE_SHARED_LIBRARY_PREFIX) # lib
SET(CMAKE_SHARED_LIBRARY_SUFFIX) # .erl
SET(CMAKE_EXECUTABLE_SUFFIX) # .elf
SET(CMAKE_DL_LIBS)

SET(CMAKE_C_OUTPUT_EXTENSION ".o")

SET(CMAKE_FIND_LIBRARY_PREFIXES "lib")
SET(CMAKE_FIND_LIBRARY_SUFFIXES ".a")

SET(CMAKE_CXX_LINK_SHARED_LIBRARY)
SET(CMAKE_CXX_LINK_MODULE_LIBRARY)
#SET(CMAKE_C_LINK_SHARED_LIBRARY)
SET(CMAKE_C_LINK_MODULE_LIBRARY)


# build EE_BIN
SET(CMAKE_C_LINK_EXECUTABLE
    "<CMAKE_C_COMPILER> <FLAGS> <CMAKE_C_LINK_FLAGS> <LINK_FLAGS> -mno-crt0 -T${EE_LINKFILE} ${EE_CRT0} <OBJECTS> -o <TARGET> <LINK_LIBRARIES> -lc -lkernel"
)

# build EE_LIB
SET(CMAKE_C_CREATE_STATIC_LIBRARY
    "<CMAKE_AR> cru <TARGET> <LINK_FLAGS> <OBJECTS>"
)

# build EE_ERL
SET(CMAKE_C_CREATE_SHARED_LIBRARY
    "<CMAKE_C_COMPILER> <CMAKE_SHARED_LIBRARY_C_FLAGS> <LANGUAGE_COMPILE_FLAGS> <LINK_FLAGS> <CMAKE_SHARED_LIBRARY_CREATE_C_FLAGS> <CMAKE_SHARED_LIBRARY_SONAME_C_FLAG><TARGET_SONAME> -o <TARGET> <OBJECTS> <LINK_LIBRARIES>"
    "${CMAKE_STRIP} --strip-unneeded -R .mdebug.eabi64 -R .reginfo -R .comment <TARGET>"
)

SET(PS2 1)
SET(EE 1)
